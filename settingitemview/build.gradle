apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
  compileSdkVersion 23
  buildToolsVersion "24.0.1"

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

group = 'me.mafei.settingitemview'
version = '1.0.0'

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile 'com.android.support:appcompat-v7:23.4.0'
}

task generateSourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath()
      .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
  from generateJavadocs.destinationDir
  classifier 'javadoc'
}

artifacts {
  archives generateJavadocsJar
  archives generateSourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  pkg {
    repo = 'maven'
    name = 'me.mafei.settingitemview'

    version {
      name = '1.0.0-tuts'
      desc = "settingitemview"
      vcsTag = '1.0.0'
    }

    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/logan62334/SettingItemView.git'
    websiteUrl = 'https://github.com/logan62334/SettingItemView'
  }

  configurations = ['archives']
}